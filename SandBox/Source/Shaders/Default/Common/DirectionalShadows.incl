vec3 DirectionalShadowCompute(vec3 normal, vec4 worldPosition)
{
	float depth = abs((u_DirectionalLight.view * worldPosition).z);

	int layer = -1;
	for (int i = 0; i < u_DirectionalLight.cascadesCount; ++i)
	{
		if (depth < u_DirectionalLight.cascadesDistance[i])
		{
			layer = i;
			break;
		}
	}
	if (layer == -1)
	{
		layer = u_DirectionalLight.cascadesCount;
	}

	vec4 lightSpacePosition = u_DirectionalLight.lightSpaceMatricies[layer] * worldPosition;
	vec3 projectedCoords = lightSpacePosition.xyz / lightSpacePosition.w;

	projectedCoords = projectedCoords * 0.5 + 0.5;

	vec3 shadow = vec3(0.0);

	float currentDepth = projectedCoords.z;
	if (currentDepth > 1.0)
	{
		return shadow;
	}

	float bias = clamp((u_DirectionalLight.bias[layer]) * tan(acos(dot(normal, u_DirectionalLight.direction))), 0.0, 0.005);
	if (layer == u_DirectionalLight.cascadesCount)
	{
		bias *= 1.0 / (u_DirectionalLight.farPlane * 0.0625);
	}
	else
	{
		bias *= 1.0 / (u_DirectionalLight.cascadesDistance[layer] * 0.5);
	}

	int pcf = layer == 0 ? u_DirectionalLight.pcf : 0;

	vec2 texelSize = u_DirectionalLight.texels / textureSize(u_DirectionalLight.CSM[layer], 0);
	for (int x = -pcf; x <= pcf; ++x)
	{
		for (int y = -pcf; y <= pcf; ++y)
		{
			float pcfDepth = texture(u_DirectionalLight.CSM[layer], projectedCoords.xy + vec2(x, y) * texelSize).r;

			if (u_DirectionalLight.isVisualized)
			{
				shadow[layer] = currentDepth - bias > pcfDepth ? 1.0 : 0.0;
			}
			else
			{
				shadow += vec3(currentDepth - bias > pcfDepth ? 1.0 : 0.0);
			}
		}
	}

	if (u_DirectionalLight.isVisualized)
	{
		shadow[layer] /= ((pcf * 2 + 1) * (pcf * 2 + 1));
	}
	else
	{
		shadow /= vec3(((pcf * 2 + 1) * (pcf * 2 + 1)));
	}

	float shadowDistance = distance(worldPosition.xyz, u_GlobalUniforms.cameraPosition);
	float farPlaneEdge = u_DirectionalLight.farPlane * (1.0 - u_DirectionalLight.fog);
	if (shadowDistance > farPlaneEdge)
	{
		float shadowFog = clamp((shadowDistance - farPlaneEdge) / (u_DirectionalLight.farPlane * u_DirectionalLight.fog), 0.0, 1.0);
		shadow *= (1.0 - shadowFog);
	}

	return shadow;
}